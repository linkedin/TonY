plugins {
  id "com.commercehub.gradle.plugin.avro" version "0.9.0"
}
apply plugin: "play"
apply plugin: "idea"
apply plugin: "distribution"

model {
  components {
    play {
      platform play: playVersion, scala: scalaVersion, java: '1.8'
      injectedRoutesGenerator = true

      sources {
        twirlTemplates {
          defaultImports = TwirlImports.JAVA
        }
      }
    }
  }
  distributions {
    playBinary {
      contents {
        from("startTHS.sh") {
          into "bin"
        }
        from("xml_to_play_opts.py") {
          into "bin"
        }
      }
      baseName = "${project.name}-${project.version}"
    }
  }
}

dependencies {
  play project(':tony-core')
  play deps.external.avro
  play deps.external.play_guice
  play deps.external.play_logback
  play deps.hadoop.hdfs_client
  play deps.hadoop.common
  play deps.hadoop.yarn_api

  playTest deps.external.assertj
  playTest deps.external.avro
  playTest deps.external.awaitility
  playTest deps.external.mockito
  playTest deps.external.testng
}

repositories {
  jcenter()
  maven {
    name "lightbend-maven-release"
    url "https://repo.lightbend.com/lightbend/maven-releases"
  }
  ivy {
    name "lightbend-ivy-release"
    url "https://repo.lightbend.com/lightbend/ivy-releases"
    layout "ivy"
  }
}

task generateAvro(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask) {
  source("avro")
  outputDir = file("build/generated-main-avro-java")
  dependencies {
    compile deps.external.avro
  }
}

compileJava {
  source generateAvro.outputs
}

test.dependsOn('testPlayBinary')


def ivyForArchives = tasks.findByPath('ivyForArchives')
if (ivyForArchives) {
  ivyForArchives.dependsOn('dist')
}
